# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
SUBSYSTEM_DIR = "../../../../.."
import("$SUBSYSTEM_DIR/core_service/telephony_core_service.gni")

rust_cxx("sim_ani_cxx_gen") {
  sources = [
    "src/wrapper.rs"
  ]
}

ohos_static_library("sim_ani_cxx") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "$SUBSYSTEM_DIR/core_service/frameworks/js/napi",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits/include",
    "${target_gen_dir}/src",
  ]

  sources = [
    "src/cxx/ani_sim.cpp",
  ]
  sources += get_target_outputs(":sim_ani_cxx_gen")
  deps = [
    ":sim_ani_cxx_gen",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits:tel_core_service_api",
    "$SUBSYSTEM_DIR/core_service/utils:libtel_common",
    "//third_party/rust/crates/cxx:cxx_cppdeps",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "napi:ace_napi",
  ]
  subsystem_name = "telephony"
  part_name = "core_service"
}

ohos_rust_shared_library("sim_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  
  external_deps = [
    "runtime_core:ani",
    "netmanager_base:ani_rs",
  ]
  deps = [
    ":sim_ani_cxx",
    "//third_party/rust/crates/serde/serde:lib",
    "//third_party/rust/crates/cxx:lib",
  ]

  clippy_lints = "none"

  subsystem_name = "telephony"
  part_name = "core_service"
  output_extension = "so"
}

generate_static_abc("sim_ani_abc") {
  base_url = "ets"
  files = [ "ets/@ohos.telephony.sim.ets" ]
  dst_file = "$target_out_dir/telephony_sim.abc"
  out_puts = [ "$target_out_dir/telephony_sim.abc" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/telephony_sim.abc"
}

ohos_prebuilt_etc("sim_ani_etc") {
  source = "$target_out_dir/telephony_sim.abc"
  module_install_dir = "framework"
  subsystem_name = "telephony"
  part_name = "core_service"
  deps = [ ":sim_ani_abc" ]
}

group("sim_ani_group") {
  deps = [
    ":sim_ani",
    ":sim_ani_etc",
  ]
}