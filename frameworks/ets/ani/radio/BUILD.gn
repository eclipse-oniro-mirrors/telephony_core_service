# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
SUBSYSTEM_DIR = "../../../../.."
import("$SUBSYSTEM_DIR/core_service/telephony_core_service.gni")

rust_cxx("telephony_radio_ani_cxx_gen") {
  sources = [
    "src/wrapper.rs"
  ]
}

ohos_static_library("telephony_radio_ani_cxx") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "$SUBSYSTEM_DIR/core_service/frameworks/js/napi",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits/include",
    "${target_gen_dir}/src",
  ]

  sources = [
    "src/cxx/ani_radio.cpp",
  ]
  sources += get_target_outputs(":telephony_radio_ani_cxx_gen")
  deps = [
    ":telephony_radio_ani_cxx_gen",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits:tel_core_service_api",
    "$SUBSYSTEM_DIR/core_service/utils:libtel_common",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
    "napi:ace_napi",
    "rust_cxx:cxx_cppdeps",
  ]

  defines = [
    "TELEPHONY_LOG_TAG = \"CoreServiceRadioJsApi\"",
    "LOG_DOMAIN = 0xD001F04",
  ]

  defines += telephony_extra_defines

  subsystem_name = "telephony"
  part_name = "core_service"
}

ohos_rust_shared_library("telephony_radio_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  
  external_deps = [
    "runtime_core:ani",
    "hilog:hilog_rust",
    "hilog:libhilog",
    "netmanager_base:ani_rs",
    "rust_cxx:lib"
  ]
  deps = [
    ":telephony_radio_ani_cxx",
    "//third_party/rust/crates/serde/serde:lib",
  ]

  clippy_lints = "none"

  subsystem_name = "telephony"
  part_name = "core_service"
  output_extension = "so"
}

generate_static_abc("telephony_radio_abc") {
  base_url = "ets"
  files = [ "ets/@ohos.telephony.radio.ets" ]
  dst_file = "$target_out_dir/telephony_radio_abc.abc"
  out_puts = [ "$target_out_dir/telephony_radio_abc.abc" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/telephony_radio_abc.abc"
}

ohos_prebuilt_etc("telephony_radio_etc") {
  source = "$target_out_dir/telephony_radio_abc.abc"
  module_install_dir = "framework"
  subsystem_name = "telephony"
  part_name = "core_service"
  deps = [ ":telephony_radio_abc" ]
}

group("telephony_radio_ani_group") {
  deps = [
    ":telephony_radio_ani",
    ":telephony_radio_etc",
  ]
}