# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

SUBSYSTEM_DIR = "../../../../.."
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("$SUBSYSTEM_DIR/core_service/telephony_core_service.gni")

subsystem_name = "telephony"
part_name = "core_service"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

copy_taihe_idl("copy_telephony_radio") {
  sources = [ "idl/ohos.telephony.radio.taihe" ] 
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_telephony_radio" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.telephony.radio.ani.cpp",
    "$taihe_generated_file_path/src/ohos.telephony.radio.abi.c",
  ]
}

taihe_shared_library("telephony_radio_taihe_native") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [
    "include",
    "$SUBSYSTEM_DIR/core_service/frameworks/js/taihe/common/include",
    "$SUBSYSTEM_DIR/core_service/interfaces/kits/native",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits/include",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.telephony.radio.impl.cpp",
    "$SUBSYSTEM_DIR/core_service/frameworks/js/taihe/common/src/ani_util.cpp",
  ]
  deps = [
    ":run_taihe",
    "$SUBSYSTEM_DIR/core_service/interfaces/innerkits:tel_core_service_api",
    "$SUBSYSTEM_DIR/core_service/utils:libtel_common",
  ]
  external_deps = [
    "ability_runtime:abilitykit_native",
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
  ]
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
}

generate_static_abc("telephony_radio_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.telephony.radio.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/telephony_radio_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("telephony_radio_etc") {
  source = "$target_out_dir/telephony_radio_abc.abc"           
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":telephony_radio_abc" ]
}
group("telephony_radio_taihe") {
  deps = [
    ":telephony_radio_etc",
    ":telephony_radio_taihe_native",
  ]
}