# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
TELEPHONY_CORE_SERVICE_ROOT = "//base/telephony/core_service"
TELEPHONY_CELLULAR_DATA_API_ROOT =
    "$TELEPHONY_CORE_SERVICE_ROOT/interfaces/innerkits/cellular_data"

config("cellular_call_util_config") {
  # header file path
  include_dirs =
      [ "$TELEPHONY_CORE_SERVICE_ROOT/interfaces/innerkits/cellular_data" ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_shared_library("tel_cellular_data_api") {
  sources = [
    "$TELEPHONY_CELLULAR_DATA_API_ROOT/cellular_data_client.cpp",
    "$TELEPHONY_CELLULAR_DATA_API_ROOT/cellular_data_service_proxy.cpp",
  ]

  include_dirs = [
    "//third_party/node/src",
    "$TELEPHONY_CORE_SERVICE_ROOT/interfaces/innerkits/core",
    "$TELEPHONY_CORE_SERVICE_ROOT/utils/log/include",
  ]

  public_configs = [ ":cellular_call_util_config" ]

  deps = [ "//utils/native/base:utils" ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr_standard:samgr_proxy",
  ]

  part_name = "core_service"
  subsystem_name = "telephony"
}
