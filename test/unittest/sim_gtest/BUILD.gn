# Copyright (C) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
SOURCE_DIR = "../../../"

declare_args() {
  sim_unittest_deps = [
    "$SOURCE_DIR/test:core_service_test_base",
    "$SOURCE_DIR/interfaces/innerkits:tel_core_service_api",
    "$SOURCE_DIR/test/mock/ffrt:ffrt_mocked",
  ]
  sim_unittest_external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_manager",
    "ability_runtime:dataobs_manager",
    "ability_runtime:data_ability_helper",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "access_token:libnativetoken_shared",
    "access_token:libtoken_setproc",
    "cJSON:cjson",
    "c_utils:utils",
    "config_policy:configpolicy_util",
    "data_share:datashare_common",
    "data_share:datashare_consumer",
    "drivers_interface_ril:ril_idl_headers",
    "eventhandler:libeventhandler",
    "googletest:gmock_main",
    "googletest:gtest_main",
    "hdf_core:libhdi",
    "hilog:libhilog",
    "i18n:zone_util",
    "init:libbegetutil",
    "ipc:ipc_single",
    "libxml2:libxml2",
    "libphonenumber:phonenumber_standard",
    "netmanager_base:net_conn_manager_if",
    "netmanager_ext:net_tether_manager_if",
    "power_manager:powermgr_client",
    "preferences:native_preferences",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "telephony_data:tel_telephony_data",
  ]
  core_service_sim_test_sources = []
  sim_unittest_targets = []
}

config("sim_unittest_config") {
  include_dirs = [
    "$SOURCE_DIR/services/core/include",
    "$SOURCE_DIR/utils/log/include",
    "$SOURCE_DIR/utils/preferences/include",
    "$SOURCE_DIR/services/sim/include",
    "$SOURCE_DIR/services/network_search/include",
    "$SOURCE_DIR/services/tel_ril/include",
    "$SOURCE_DIR/test/mock",
  ]
}

core_service_sim_test_sources += [
  "core_manager_inner_gtest.cpp",
  "csim_file_controller_gtest.cpp",
  "isim_file_gtest.cpp",
  "multi_sim_controller_gtest.cpp",
  "operator_config_loader_gtest.cpp",
  "operator_matching_rule_test.cpp",
  "pdp_profile_rdb_helper_test.cpp",
  "operator_config_cache_test.cpp",
  "ruim_file_gtest.cpp",
  "sim_file_gtest.cpp",
  "sim_manager_gtest.cpp",
  "sim_sms_controller_gtest.cpp",
  "sim_utils_gtest.cpp",
  "usim_dialling_numbers_service_gtest.cpp",
]

template("sim_unittest_suite") {
  ohos_unittest(target_name) {
    use_exceptions = true
    install_enable = true
    module_out_path = "core_service/core_service_sim"
    sources = invoker.sources
    deps = sim_unittest_deps
    external_deps = sim_unittest_external_deps
    subsystem_name = "telephony"
    part_name = "core_service"
    configs = [ "$SOURCE_DIR/utils:telephony_log_config" ]
    configs += [ ":sim_unittest_config" ]
    defines = [
    "TELEPHONY_LOG_TAG = \"CoreServiceGtest\"",
    "LOG_DOMAIN = 0xD000F00",
  ]


    if (defined(global_parts_info) &&
        defined(global_parts_info.location_location) &&
        global_parts_info.location_location) {
      external_deps += [
        "location:lbsservice_common",
        "location:locator_sdk",
      ]
      defines += [ "ABILITY_LOCATION_SUPPORT" ]
    }

    cflags = [
      "-flto",
      "-fvisibility=hidden",
    ]

    ldflags = [ "-flto" ]
  }
}

foreach(source, core_service_sim_test_sources) {
  name = get_path_info(source, "name")
  sim_unittest_suite("sim_unittest_$name") {
    sources = [ source ]
  }
  sim_unittest_targets += [ ":sim_unittest_$name" ]
}

group("sim_gtest") {
  testonly = true
  deps = sim_unittest_targets
}
